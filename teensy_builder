#!/usr/bin/perl

use strict;
use warnings;

use Config::IniFiles;
use File::Basename 'basename';
use File::Path 'make_path';
use File::Spec::Functions 'catfile', 'catdir';

my %conf;
my $conf = Config::IniFiles->new(-file => 'builder.conf');
$conf = Config::IniFiles->new(-file => 'local.conf', -import => $conf);

my %targets = (
  teensy31 => {
    core => 'mk20dx256',
    rom_size => 262144,
    ram_size => 65536,
    valid_freq => [2, 4, 8, 16, 24, 48, 72, 96],
    cpu => 'cortex-m4',
    base_libs => '-larm_cortexM4l_math -lm',
  },
  teensy30 => {
    core => 'mk20dx128',
    rom_size => 131072,
    ram_size => 16384,
    valid_freq => [24, 48, 96],
    cpu => 'cortex-m4',
    base_libs => '-larm_cortexM4l_math -lm',
  },
  teensylc => {
    core => 'mkl26z64',
    rom_size => 63488,
    ram_size => 8192,
    valid_freq => [24, 48],
    cpu => 'cortex-m0plus',
    base_libs => '-larm_cortexM0l_math -lm',
  },
);
$targets{teensy32} = $targets{teensy31};

my @usb_types = qw(SERIAL HID SERIAL_HID MIDI RAW_HID FLIGHTSIM DISABLED);
my @keyboard_layouts =
  qw(US_ENGLISH CANADIAN_FRENCH CANADIAN_MULTILINGUAL CZECH
     DANISH FINNISH FRENCH FRENCH_BELGIAN FRENCH_SWISS GERMAN
     GERMAN_MAC GERMAN_SWISS ICELANDIC IRISH ITALIAN NORWEGIAN
     PORTUGUESE PORTUGUESE_BRAZILIAN SERBIAN_LATIN_ONLY SPANISH
     SPANISH_LATIN_AMERICA SWEDISH TURKISH UNITED_KINGDOM
     US_INTERNATIONAL);

die "You must specify a target.\n" if not $conf->exists('main', 'target');
die "Invalid target: ".$conf->val('main', 'target')."\n" if not exists $targets{$conf->val('main', 'target')};
my %target = %{$targets{$conf->val('main', 'target')}};
my $usb_type = uc($conf->val('main', 'usbtype', 'SERIAL'));
die "Invalid usb type: ${usb_type}.\n" if not grep { $_ eq $usb_type } @usb_types;
my $keyboard_layout = uc($conf->val('main', 'keyboard', 'US_ENGLISH'));
die "Invalid keyboard layout: ${keyboard_layout}.\n" if not grep { $_ eq $keyboard_layout } @keyboard_layouts;
my $cpu_freq = $conf->val('main', 'cpufreq', 48);
die "Invalid CPU frequency: ${cpu_freq}.\n" if not grep { $_ == $cpu_freq } @{$target{valid_freq}};

my @libs = $conf->Parameters('libs');
die "You must specify the 'teensy_core' library.\n" if not grep { $_ eq 'teensy_core' } @libs;

sub common_flags {
  my $config = sprintf '-DF_CPU=%d000000 -DUSB_%s -DLAYOUT_%s -D__%s__ -mcpu=%s',
    $cpu_freq, $usb_type, $keyboard_layout, uc($target{core}), $target{cpu};
  my $base = '-DUSING_MAKEFILE -DARDUINO_ARCH_AVR -DARDUINO=10605 -DTEENSYDUINO=125';
  my $flags = '-Wall -g -ffunction-sections -fdata-sections -nostdlib -Os -mthumb -fsingle-precision-constant -MMD';
  my @includes =  ('-I.', map { '-I'.$conf->val('libs', $_) } @libs);
  return $config.' '.$base.' '.$flags.' '.join(' ', @includes);
}

sub cxx_flags {
  return common_flags().' -std=gnu++0x -felide-constructors -fno-exceptions -fno-rtti';
}

sub ld_flags {
 my $flags = sprintf '-Wl,--gc-sections,--relax,--defsym=__rtc_localtime=%d -mcpu=%s -T %s',
   time(), $target{cpu}, catfile($conf->val('libs', 'teensy_core'), $target{core}.'.ld');
 return $flags.' -Os --specs=nano.specs -mthumb',
}

my %extensions = (
  c => ['gcc', common_flags()],
  cpp => ['g++', cxx_flags()],
  S => ['gcc', common_flags()],
);

sub compiler {
  my ($name) = @_;
  my $bin =  $conf->val('paths', 'compiler_prefix', 'arm-none-eabi-').$name;
  return "'".catfile($conf->val('paths', 'compiler'), $bin)."' ";
}

sub tool {
  my ($name) = @_;
  return "'".catfile($conf->val('paths', 'tools'), $name)."' ";
}

sub run {
  my ($cmd, $error_msg) = @_;
  print "$cmd\n";
  system $cmd and die $error_msg."\n";
}

sub mtime {
  my ($file) = @_;
  if (not -f $file or not (my @st = stat(_))) {
    return 0;
  } else {
    return $st[9];
  }
}

sub should_build {
  my ($file) = @_;
  my $dest_time = mtime($file);
  return 1 if not $dest_time;
  my $dep_file = $file =~ s/\.o$/.d/r;
  return 1 if not -f $dep_file;
  local $/ = undef;
  open my $fh, '<', $dep_file;
  my (undef, @deps) = split(/\s*:?\s+\\?\r?\n?\s*/, <$fh>);
  close $fh;
  for my $f (@deps) { # $dep->targets()) {
    return 1 if $dest_time < mtime($f);
  }
  return 0;
}

for my $lib (@libs) {
  print "Compiling lib $lib (".$conf->val('libs', $lib).").\n";
  my $indir = $conf->val('libs', $lib);
  my $outdir = catdir('_build/', $lib);
  my $outlib = catfile($outdir, $lib.'.a');
  make_path($outdir);
  for my $ext (keys %extensions) {
    # print "Looking for files with extension: $ext.\n";
    my ($tool, $flags) = @{$extensions{$ext}};
    my $compiler = compiler($tool);
    my @allfiles = glob(catfile($indir, '*.'.$ext));
    for my $f (@allfiles) {
      my $out = catfile($outdir, basename($f).'.o');
      next if $lib eq 'teensy_core' && basename($f) eq 'main.cpp';
      if (should_build($out)) {
        print "Building: $f\n";
        run("$compiler $flags -o $out -c $f", "Compilation failed.");
        run(compiler('ar')." rcs $outlib $out", "Cannot add '$out' to the library '$outlib'.");
      } else {
        print "Target up-to-date: $out\n";
      }
    }
  }
}

sub check_size {
  my ($elf) = @_;
  my $cmd = compiler('size')." -A $elf";
  print "$cmd\n";
  my %size = map { print; m/^(\S+)\s+(\S+).*$/ } qx/$cmd/;
  my $rom = $size{'.text'} + $size{'.data'};
  my $ram = $size{'.data'} + $size{'.bss'};
  printf "\nUsing %d bytes of ROM (%d%% of %d bytes).\n", $rom, (100 * $rom / $target{rom_size}), $target{rom_size};
  printf "Using %d bytes of RAM (%d%% of %d bytes).\n\n", $ram, (100 * $ram / $target{ram_size}), $target{ram_size};
  return $rom <= $target{rom_size} && $ram <= $target{ram_size};
}

my $alllibs = join(' ', map {catfile(catdir('_build', $_), $_.'.a')} (reverse @libs));
my $outname = $conf->val('main', 'name', 'teensy_prog');
my $elf = catfile('_build', $outname.'.elf');
my $hex = catfile('_build', $outname.'.hex');
my $eep = catfile('_build', $outname.'.eep');
run(compiler('gcc').ld_flags()." $alllibs -o $elf", "Cannot build ELF file.");
check_size($elf) or die "The program cannot fit in the memory of your board!ll\n";
run(compiler('objcopy')."-O ihex -R .eeprom $elf $hex", "Cannot build HEX file.");
run(compiler('objcopy')."-O ihex -j .eeprom --set-section-flags=.eeprom=alloc,load --no-change-warnings --change-section-lma .eeprom=0 $elf $eep", "Cannot build eeprom file.");
run(tool('teensy_post_compile')."-file=$outname -path=_build -tools='".$conf->val('paths', 'tools')."'", "Error while running teensy_pos_compile.");
run(tool('teensy_reboot'), "");
